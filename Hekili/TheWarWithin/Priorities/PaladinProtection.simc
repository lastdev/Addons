actions.precombat+=/devotion_aura
actions.precombat+=/lights_judgment
actions.precombat+=/arcane_torrent
actions.precombat+=/consecration
actions.precombat+=/variable,name=trinket_sync_slot,value=1,if=trinket.1.has_cooldown&trinket.1.has_stat.any_dps&(!trinket.2.has_stat.any_dps|trinket.1.cooldown.duration>=trinket.2.cooldown.duration)|!trinket.2.has_cooldown
actions.precombat+=/variable,name=trinket_sync_slot,value=2,if=trinket.2.has_cooldown&trinket.2.has_stat.any_dps&(!trinket.1.has_stat.any_dps|trinket.2.cooldown.duration>trinket.1.cooldown.duration)|!trinket.1.has_cooldown

actions+=/rebuke
actions+=/call_action_list,name=cooldowns
actions+=/call_action_list,name=trinkets
actions+=/call_action_list,name=standard

actions.cooldowns+=/lights_judgment,if=spell_targets.lights_judgment>=2|!raid_event.adds.exists|raid_event.adds.in>75|raid_event.adds.up
actions.cooldowns+=/avenging_wrath
actions.cooldowns+=/potion,if=buff.avenging_wrath.up
actions.cooldowns+=/moment_of_glory,if=(buff.avenging_wrath.remains<15|(time>10|(cooldown.avenging_wrath.remains>15))&(cooldown.avengers_shield.remains&cooldown.judgment.remains&cooldown.hammer_of_wrath.remains))
actions.cooldowns+=/divine_toll,if=spell_targets.shield_of_the_righteous>=3
actions.cooldowns+=/bastion_of_light,if=buff.avenging_wrath.up|cooldown.avenging_wrath.remains<=30
## actions.cooldowns+=/invoke_external_buff,name=power_infusion,if=buff.avenging_wrath.up
actions.cooldowns+=/fireblood,if=buff.avenging_wrath.remains>8

actions.standard+=/hammer_of_light,if=!buff.shake_the_heavens.up|(buff.shake_the_heavens.up&buff.shake_the_heavens.remains<2.4)|(buff.hammer_of_light_free.up&buff.hammer_of_light_free.remains<3)|(buff.hammer_of_light_ready.up&buff.hammer_of_light_ready.remains<3)
# Use Shield of the Righteous according to Righteous Protector's ICD, but use it asap if it's a free proc (Bugged interaction, this ignores ICD).
actions.standard+=/shield_of_the_righteous,if=(((!talent.righteous_protector.enabled|cooldown.righteous_protector_icd.remains=0)&holy_power>2)|buff.bastion_of_light.up|buff.divine_purpose.up)&!buff.hammer_of_light_ready.up
actions.standard+=/holy_armaments,if=next_armament=sacred_weapon&(!buff.sacred_weapon.up|(buff.sacred_weapon.remains<6&!buff.avenging_wrath.up))
actions.standard+=/judgment,cycle_targets=1,if=spell_targets.shield_of_the_righteous>3&buff.bulwark_of_righteous_fury.stack>=3&holy_power<3
actions.standard+=/eye_of_tyr,if=talent.lights_guidance.enabled
actions.standard+=/hammer_of_wrath
actions.standard+=/judgment,cycle_targets=1,if=charges>=2|full_recharge_time<=gcd.max
actions.standard+=/avengers_shield,if=(spell_targets.avengers_shield>2|buff.moment_of_glory.up)&!buff.shake_the_heavens.up
actions.standard+=/divine_toll,if=(!raid_event.adds.exists|raid_event.adds.in>10)
actions.standard+=/judgment,cycle_targets=1
actions.standard+=/avengers_shield
actions.standard+=/consecration,if=!consecration.up
actions.standard+=/eye_of_tyr,if=talent.inmost_light.enabled&raid_event.adds.in>=45|spell_targets.shield_of_the_righteous>=3
# Takes advantage of a bug which guarantees Divine Inspiration to give a sacred weapon if the current armament is bulwark
actions.standard+=/holy_armaments,if=next_armament=holy_bulwark&charges=2&cooldown.avenging_wrath.remains>=30
actions.standard+=/holy_armaments,if=next_armament=sacred_weapon&(buff.sacred_weapon.remains<6&buff.avenging_wrath.up)
actions.standard+=/blessed_hammer
actions.standard+=/hammer_of_the_righteous
actions.standard+=/crusader_strike
actions.standard+=/eye_of_tyr,if=!talent.inmost_light.enabled&raid_event.adds.in>=60|spell_targets.shield_of_the_righteous>=3
actions.standard+=/word_of_glory,if=buff.shining_light_free.up
actions.standard+=/arcane_torrent,if=holy_power<5
actions.standard+=/consecration

actions.trinkets+=/use_items,slots=trinket1,if=(variable.trinket_sync_slot=1&(buff.avenging_wrath.up|fight_remains<=40)|(variable.trinket_sync_slot=2&(!trinket.2.cooldown.ready|!buff.avenging_wrath.up))|!variable.trinket_sync_slot)
actions.trinkets+=/use_items,slots=trinket2,if=(variable.trinket_sync_slot=2&(buff.avenging_wrath.up|fight_remains<=40)|(variable.trinket_sync_slot=1&(!trinket.1.cooldown.ready|!buff.avenging_wrath.up))|!variable.trinket_sync_slot)
