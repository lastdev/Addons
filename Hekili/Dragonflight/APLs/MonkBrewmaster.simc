actions.precombat+=/potion
actions.precombat+=/chi_burst,if=talent.chi_burst.enabled
actions.precombat+=/chi_wave,if=talent.chi_wave.enabled
actions.precombat+=/summon_white_tiger_statue,if=talent.summon_white_tiger_statue.enabled

actions+=/spear_hand_strike
actions+=/diffuse_magic
actions+=/purifying_brew,if=settings.purify_for_celestial&(time_to_max_charges<gcd.max|buff.purified_chi.up&buff.purified_chi.remains<1.5*gcd.max)|cooldown.celestial_brew.remains<2*gcd.max&charges_fractional>1.5
actions+=/celestial_brew,if=tanking&(!talent.improved_celestial_brew|buff.purified_chi.up)&buff.blackout_combo.down
actions+=/purifying_brew,line_cd=5,if=talent.improved_invoke_niuzao_the_black_ox&settings.purify_for_niuzao&pet.niuzao_the_black_ox.active
# Use configured purify_stagger_currhp from Brewmaster options.
actions+=/purifying_brew,if=group&buff.blackout_combo.down&settings.purify_stagger_currhp>0&stagger.pct>=settings.purify_stagger_currhp
# Use configured purify_stagger_currhp from Brewmaster options.
actions+=/purifying_brew,if=solo&buff.blackout_combo.down&settings.purify_stagger_currhp>0&stagger.pct>=settings.purify_stagger_currhp*0.5
# Use configured purify_stagger_maxhp from Brewmaster options.
actions+=/purifying_brew,if=group&buff.blackout_combo.down&settings.purify_stagger_maxhp>0&stagger.pct>=settings.purify_stagger_maxhp
# Use configured purify_stagger_maxhp from Brewmaster options.
actions+=/purifying_brew,if=solo&buff.blackout_combo.down&settings.purify_stagger_maxhp>0&stagger.pct>=settings.purify_stagger_maxhp*0.5
# Stagger % of 20 will kill you in 2.5 seconds without healing or purifying.  Used when purify_stagger_currhp and maxhp are disabled.
actions+=/purifying_brew,if=group&buff.blackout_combo.down&settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&stagger.pct>20
# Stagger % of 10 will kill you in 5 seconds without healing or purifying.  Used when purify_stagger_currhp and maxhp are disabled.
actions+=/purifying_brew,if=solo&buff.blackout_combo.down&settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&stagger.pct>10
actions+=/dampen_harm,if=health.pct<75&incoming_damage_3s>health.max*(0.2+(0.2*group))&buff.fortifying_brew.down
actions+=/fortifying_brew,if=health.pct<50&incoming_damage_3s>health.max*(0.2+(0.2*group))&(buff.dampen_harm.down)
actions+=/potion
### Use <a href='https://www.wowhead.com/spell=10060/power-infusion'>Power Infusion</a> when <a href='https://www.wowhead.com/spell=387184/weapons-of-order'>Weapons of Order</a> reaches 4 stacks.
## actions+=/invoke_external_buff,name=power_infusion,if=buff.weapons_of_order.remains<=20&talent.weapons_of_order.enabled
## actions+=/invoke_external_buff,name=power_infusion,if=!talent.weapons_of_order.enabled
actions+=/touch_of_death
actions+=/use_items,if=debuff.weapons_of_order_debuff.stack>3|talent.weapons_of_order|boss&fight_remains<25
actions+=/blood_fury
actions+=/berserking
actions+=/lights_judgment
actions+=/fireblood
actions+=/ancestral_call
actions+=/bag_of_tricks
actions+=/call_action_list,name=rotation_pta,strict=1,if=talent.press_the_advantage.enabled
actions+=/call_action_list,name=rotation_boc,strict=1,if=!talent.press_the_advantage.enabled

actions.rotation_pta+=/invoke_niuzao_the_black_ox,if=debuff.weapons_of_order_debuff.stack>3
actions.rotation_pta+=/invoke_niuzao_the_black_ox,if=!talent.weapons_of_order.enabled
actions.rotation_pta+=/rising_sun_kick,if=(buff.press_the_advantage.stack<6|buff.press_the_advantage.stack>9)&active_enemies<=4
actions.rotation_pta+=/keg_smash,if=(buff.press_the_advantage.stack<8|buff.press_the_advantage.stack>9)&active_enemies>4
actions.rotation_pta+=/blackout_kick
## actions.rotation_pta+=/purifying_brew,if=(!buff.blackout_combo.up)
actions.rotation_pta+=/black_ox_brew,if=energy+energy.regen<=40
actions.rotation_pta+=/summon_white_tiger_statue,if=debuff.weapons_of_order_debuff.stack>3|!talent.weapons_of_order.enabled
# Modified: If WoO is on cooldown and BDB will be back up before WoO's CD expires, go ahead and use BDB.
actions.rotation_pta+=/bonedust_brew,if=talent.weapons_of_order&(buff.weapons_of_order.down&cooldown.weapons_of_order.true_remains>cooldown|time<10&debuff.weapons_of_order_debuff.stack>3|time>10)|!talent.weapons_of_order.enabled
actions.rotation_pta+=/exploding_keg,if=buff.bonedust_brew.up|!talent.bonedust_brew.enabled
actions.rotation_pta+=/breath_of_fire,if=!(buff.press_the_advantage.stack>6&buff.blackout_combo.up)
actions.rotation_pta+=/keg_smash,if=!(buff.press_the_advantage.stack>6&buff.blackout_combo.up)
actions.rotation_pta+=/rushing_jade_wind,if=talent.rushing_jade_wind.enabled
actions.rotation_pta+=/spinning_crane_kick,if=active_enemies>1
actions.rotation_pta+=/expel_harm
actions.rotation_pta+=/chi_wave,if=talent.chi_wave.enabled
actions.rotation_pta+=/chi_burst,if=talent.chi_burst.enabled

actions.rotation_boc+=/blackout_kick
actions.rotation_boc+=/invoke_niuzao_the_black_ox,if=debuff.weapons_of_order_debuff.stack>3
actions.rotation_boc+=/invoke_niuzao_the_black_ox,if=!talent.weapons_of_order.enabled
actions.rotation_boc+=/weapons_of_order
actions.rotation_boc+=/keg_smash,if=action.weapons_of_order.time_since<2&talent.weapons_of_order.enabled
## actions.rotation_boc+=/purifying_brew,if=(!buff.blackout_combo.up)
actions.rotation_boc+=/rising_sun_kick
actions.rotation_boc+=/keg_smash,if=buff.weapons_of_order.up&debuff.weapons_of_order_debuff.remains<=gcd*2
actions.rotation_boc+=/black_ox_brew,if=energy+energy.regen<=40
actions.rotation_boc+=/tiger_palm,if=buff.blackout_combo.up&active_enemies=1
actions.rotation_boc+=/breath_of_fire,if=buff.charred_passions.remains<cooldown.blackout_kick.remains
actions.rotation_boc+=/breath_of_fire,if=buff.charred_passions.down
actions.rotation_boc+=/keg_smash,if=buff.weapons_of_order.up&debuff.weapons_of_order_debuff.stack<=3
actions.rotation_boc+=/summon_white_tiger_statue,if=debuff.weapons_of_order_debuff.stack>3|!talent.weapons_of_order.enabled
# Modified: If WoO is on cooldown and BDB will be back up before WoO's CD expires, go ahead and use BDB.
actions.rotation_boc+=/bonedust_brew,if=talent.weapons_of_order&(buff.weapons_of_order.down&cooldown.weapons_of_order.true_remains>cooldown|time<10&debuff.weapons_of_order_debuff.stack>3|time>10)|!talent.weapons_of_order.enabled
actions.rotation_boc+=/exploding_keg,if=buff.bonedust_brew.up|!talent.bonedust_brew.enabled
actions.rotation_boc+=/keg_smash
actions.rotation_boc+=/rushing_jade_wind,if=talent.rushing_jade_wind.enabled
actions.rotation_boc+=/breath_of_fire
actions.rotation_boc+=/tiger_palm,if=active_enemies=1&!talent.blackout_combo.enabled
actions.rotation_boc+=/spinning_crane_kick,if=active_enemies>1
actions.rotation_boc+=/expel_harm
actions.rotation_boc+=/chi_wave,if=talent.chi_wave.enabled
actions.rotation_boc+=/chi_burst,if=talent.chi_burst.enabled