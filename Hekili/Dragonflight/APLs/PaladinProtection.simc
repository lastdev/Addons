actions.precombat+=/devotion_aura,if=buff.paladin_aura.down
actions.precombat+=/lights_judgment
actions.precombat+=/arcane_torrent
actions.precombat+=/consecration

# Executed every time the actor is available.
actions=rebuke
actions+=/cleanse_toxins
actions+=/call_action_list,name=cooldowns
actions+=/call_action_list,name=mitigation
actions+=/call_action_list,name=trinkets
actions+=/call_action_list,name=standard

# Use Avenger's Shield as first priority before anything else, if t29 2pc is equipped.
actions.cooldowns+=/avengers_shield,if=time=0&(set_bonus.tier29_2pc|set_bonus.df4_2pc)
actions.cooldowns+=/lights_judgment,if=spell_targets.lights_judgment>=2|!raid_event.adds.exists|raid_event.adds.in>75|raid_event.adds.up
actions.cooldowns+=/avenging_wrath
actions.cooldowns+=/sentinel,if=!defensive_sentinel
actions.cooldowns+=/potion,if=buff.aw_sentinel.up
actions.cooldowns+=/moment_of_glory,if=(buff.aw_sentinel.remains<15|(time>10|cooldown.sentinel.remains>15|cooldown.avenging_wrath.remains>15)&(cooldown.avengers_shield.remains&cooldown.judgment.remains&cooldown.hammer_of_wrath.remains))
actions.cooldowns+=/divine_toll,if=spell_targets.shield_of_the_righteous>=3
actions.cooldowns+=/bastion_of_light,if=buff.aw_sentinel.up|talent.sentinel&cooldown.sentinel.remains<=30|!talent.sentinel&cooldown.avenging_wrath.remains<=30
## actions.cooldowns+=/invoke_external_buff,name=power_infusion,if=buff.aw_sentinel.up
actions.cooldowns+=/fireblood,if=buff.aw_sentinel.remains>8

actions.mitigation=shield_of_the_righteous,if=tanking&buff.shield_of_the_righteous.down&(holy_power.deficit=0|buff.divine_purpose.up)
actions.mitigation+=/word_of_glory,if=health.pct<wog_health&(holy_power.deficit=0|buff.divine_purpose.up|buff.shining_light_free.up)
actions.mitigation+=/divine_shield,if=talent.final_stand.enabled&tanking&incoming_damage_5s>ds_damage&!(buff.ardent_defender.up|buff.guardian_of_ancient_kings.up|buff.divine_shield.up|buff.potion.up)
actions.mitigation+=/guardian_of_ancient_kings,if=tanking&incoming_damage_5s>goak_damage&!(buff.ardent_defender.up|buff.guardian_of_ancient_kings.up|buff.divine_shield.up|buff.potion.up)
actions.mitigation+=/sentinel,if=defensive_sentinel&tanking&incoming_damage_5s>goak_damage&!(buff.ardent_defender.up|buff.guardian_of_ancient_kings.up|buff.divine_shield.up|buff.potion.up)
actions.mitigation+=/ardent_defender,if=tanking&incoming_damage_5s>goak_damage&!(buff.ardent_defender.up|buff.guardian_of_ancient_kings.up|buff.divine_shield.up|buff.potion.up)
actions.mitigation+=/lay_on_hands,if=health.pct<15

actions.standard+=/consecration,if=buff.sanctification.stack=buff.sanctification.max_stack
# Use Shield of the Righteous according to Righteous Protector's ICD, but use it asap if it's a free proc (Bugged interaction, this ignores ICD). Don't use it when on max Sanctification Stacks (Very next GCD will trigger Consecration, so we want the bonus damage)
actions.standard+=/shield_of_the_righteous,if=(((!talent.righteous_protector.enabled|cooldown.righteous_protector_icd.remains=0)&holy_power>2)|buff.bastion_of_light.up|buff.divine_purpose.up)&(!buff.sanctification.up|buff.sanctification.stack<buff.sanctification.max_stack)
# Use Avenger's Shield as First Priority when 2pc buff is missing.
actions.standard+=/avengers_shield,if=(set_bonus.tier29_2pc|set_bonus.df4_2pc)&(!buff.ally_of_the_light.up|buff.ally_of_the_light.remains<gcd.execute)
actions.standard+=/judgment,cycle_targets=1,if=spell_targets.shield_of_the_righteous>3&buff.bulwark_of_righteous_fury.stack>=3&holy_power<3
# Use Judgment with higher priority if we need to build Sanctification Stacks
actions.standard+=/judgment,cycle_targets=1,if=!buff.sanctification_empower.up&set_bonus.tier31_2pc
actions.standard+=/hammer_of_wrath
actions.standard+=/judgment,cycle_targets=1,if=charges>=2|full_recharge_time<=gcd.max
actions.standard+=/avengers_shield,if=spell_targets.avengers_shield>2|buff.moment_of_glory.up
actions.standard+=/divine_toll,if=(!raid_event.adds.exists|raid_event.adds.in>10)
actions.standard+=/avengers_shield
actions.standard+=/hammer_of_wrath
actions.standard+=/judgment,cycle_targets=1
actions.standard+=/consecration,if=!consecration.up&(!buff.sanctification.stack=buff.sanctification.max_stack|!set_bonus.tier31_2pc)
actions.standard+=/eye_of_tyr,if=talent.inmost_light.enabled&raid_event.adds.in>=45|spell_targets.shield_of_the_righteous>=3
actions.standard+=/blessed_hammer
actions.standard+=/hammer_of_the_righteous
actions.standard+=/crusader_strike
actions.standard+=/eye_of_tyr,if=!talent.inmost_light.enabled&raid_event.adds.in>=60|spell_targets.shield_of_the_righteous>=3
actions.standard+=/word_of_glory,if=buff.shining_light_free.up
actions.standard+=/arcane_torrent,if=holy_power<5
actions.standard+=/consecration,if=!buff.sanctification_empower.up

actions.trinkets+=/variable,name=trinket_sync_slot,value=1,if=trinket.1.has_cooldown&trinket.1.has_stat.any_dps&(!trinket.2.has_stat.any_dps|trinket.1.cooldown.duration>=trinket.2.cooldown.duration)|!trinket.2.has_cooldown
actions.trinkets+=/variable,name=trinket_sync_slot,value=2,if=trinket.2.has_cooldown&trinket.2.has_stat.any_dps&(!trinket.1.has_stat.any_dps|trinket.2.cooldown.duration>trinket.1.cooldown.duration)|!trinket.1.has_cooldown
actions.trinkets+=/use_items,slots=trinket1,if=(variable.trinket_sync_slot=1&(buff.aw_sentinel.up|boss&fight_remains<=40)|(variable.trinket_sync_slot=2&(!trinket.2.cooldown.ready|!buff.aw_sentinel.up))|!variable.trinket_sync_slot)
actions.trinkets+=/use_items,slots=trinket2,if=(variable.trinket_sync_slot=2&(buff.aw_sentinel.up|boss&fight_remains<=40)|(variable.trinket_sync_slot=1&(!trinket.1.cooldown.ready|!buff.aw_sentinel.up))|!variable.trinket_sync_slot)
actions.trinkets+=/use_items