actions.precombat=fel_domination,if=time>0&!pet.alive
actions.precombat+=/summon_pet
actions.precombat+=/use_item,name=tome_of_monstrous_constructions
actions.precombat+=/use_item,name=soleahs_secret_technique
actions.precombat+=/grimoire_of_sacrifice,if=talent.grimoire_of_sacrifice.enabled
actions.precombat+=/inquisitors_gaze
actions.precombat+=/fleshcraft
actions.precombat+=/use_item,name=shadowed_orb_of_torment
actions.precombat+=/soul_fire
actions.precombat+=/incinerate

actions=spell_lock
actions=call_action_list,name=havoc,strict=1,if=havoc_active&active_enemies>1&active_enemies<5-talent.inferno.enabled+(talent.inferno.enabled&talent.internal_combustion.enabled)
actions+=/fleshcraft,if=soulbind.volatile_solvent,interrupt_if=buff.volatile_solvent.up
actions+=/conflagrate,if=talent.roaring_blaze.enabled&debuff.roaring_blaze.remains<1.5
actions+=/cataclysm
actions+=/run_action_list,name=aoe,strict=1,if=settings.fixed_aoe_3_plus&active_enemies>2|!settings.fixed_aoe_3_plus&active_enemies>2-talent.ritual_of_ruin.rank
actions+=/soul_fire,cycle_targets=1,if=refreshable&soul_shard<=4&(!talent.cataclysm.enabled|cooldown.cataclysm.remains>remains)
actions+=/immolate,cycle_targets=1,if=remains<3&target.time_to_die>8&(!talent.cataclysm.enabled|cooldown.cataclysm.remains>remains)
actions+=/immolate,if=target.time_to_die>8&talent.internal_combustion.enabled&action.chaos_bolt.in_flight&remains<duration*0.5
actions+=/chaos_bolt,if=(pet.infernal.active|pet.blasphemy.active)&soul_shard>=4
actions+=/call_action_list,name=cds
actions+=/channel_demonfire
actions+=/scouring_tithe
actions+=/decimating_bolt
actions+=/havoc,cycle_targets=1,if=dot.immolate.remains>dot.immolate.duration*0.5|!talent.internal_combustion.enabled
actions+=/impending_catastrophe
actions+=/soul_rot
actions+=/havoc,if=runeforge.odr_shawl_of_the_ymirjar.equipped
actions+=/variable,name=pool_soul_shards,value=active_enemies>1&cooldown.havoc.remains<=10|buff.ritual_of_ruin.up&talent.rain_of_chaos
actions+=/conflagrate,if=buff.backdraft.down&soul_shard>=1.5-0.3*talent.flashover.enabled&!variable.pool_soul_shards
actions+=/chaos_bolt,if=pet.infernal.active|buff.rain_of_chaos.remains>cast_time
actions+=/chaos_bolt,if=buff.backdraft.up&!variable.pool_soul_shards
actions+=/chaos_bolt,if=talent.eradication&!variable.pool_soul_shards&debuff.eradication.remains<cast_time
actions+=/shadowburn,if=!variable.pool_soul_shards|soul_shard>=4.5
actions+=/chaos_bolt,if=soul_shard>3.5
actions+=/chaos_bolt,if=boss&fight_remains<5&target.time_to_die>cast_time+travel_time
actions+=/conflagrate,if=charges>1|target.time_to_die<gcd
actions+=/dimensional_rift,if=soul_shard<4.5
# It takes 7 impacts of Soulkeeper to exceed 1 Incinerate.
actions+=/summon_soulkeeper,if=!havoc_active&buff.tormented_souls.stack*active_enemies>6
actions+=/incinerate

actions.aoe=rain_of_fire,if=pet.infernal.active&(!cooldown.havoc.ready|active_enemies>3)
actions.aoe+=/rain_of_fire,if=talent.ritual_of_ruin.enabled
actions.aoe+=/soul_rot
actions.aoe+=/impending_catastrophe
actions.aoe+=/channel_demonfire,if=dot.immolate.remains>cast_time
actions.aoe+=/immolate,cycle_targets=1,if=active_enemies<5&target.time_to_die>8&remains<5&(!talent.cataclysm.enabled|cooldown.cataclysm.remains>remains)
actions.aoe+=/call_action_list,name=cds
actions.aoe+=/havoc,cycle_targets=1,if=active_enemies<4
actions.aoe+=/rain_of_fire
actions.aoe+=/havoc,cycle_targets=1
actions.aoe+=/decimating_bolt
actions.aoe+=/incinerate,if=talent.fire_and_brimstone.enabled&buff.backdraft.up&soul_shard<5-0.2*active_enemies
actions.aoe+=/soul_fire
actions.aoe+=/conflagrate,if=buff.backdraft.down
actions.aoe+=/shadowburn,if=target.health.pct<20
actions.aoe+=/immolate,cycle_targets=1,if=target.time_to_die>8&refreshable
actions.aoe+=/scouring_tithe
actions.aoe+=/dimensional_rift,if=soul_shard<4.5
# It takes 7 impacts of Soulkeeper to exceed 1 Incinerate.
actions.aoe+=/summon_soulkeeper,if=!havoc_active&buff.tormented_souls.stack*active_enemies>6
actions.aoe+=/incinerate

actions.cds=use_item,name=shadowed_orb_of_torment,if=cooldown.summon_infernal.remains<3|boss&time_to_die<42
actions.cds+=/summon_infernal
## actions.cds+=/dark_soul_instability,if=pet.infernal.active|boss&cooldown.summon_infernal.remains_expected>time_to_die
actions.cds+=/potion,if=pet.infernal.active
actions.cds+=/berserking,if=pet.infernal.active
actions.cds+=/blood_fury,if=pet.infernal.active
actions.cds+=/fireblood,if=pet.infernal.active
actions.cds+=/use_item,name=scars_of_fraternal_strife,if=!buff.scars_of_fraternal_strife_4.up
actions.cds+=/use_item,name=scars_of_fraternal_strife,if=buff.scars_of_fraternal_strife_4.up&pet.infernal.active
actions.cds+=/use_items,if=pet.infernal.active|boss&time_to_die<21

actions.havoc=conflagrate,if=buff.backdraft.down&soul_shard>=1&soul_shard<=4
actions.havoc+=/soul_fire,if=cast_time<havoc_remains
actions.havoc+=/decimating_bolt,if=cast_time<havoc_remains&soulbind.lead_by_example.enabled
actions.havoc+=/scouring_tithe,if=cast_time<havoc_remains
actions.havoc+=/immolate,if=target.time_to_die>8&talent.internal_combustion.enabled&remains<duration*0.5|!talent.internal_combustion.enabled&refreshable
actions.havoc+=/chaos_bolt,if=cast_time<havoc_remains&!(talent.ritual_of_ruin.enabled&active_enemies>1&talent.inferno.enabled)
actions.havoc+=/rain_of_fire,if=set_bonus.tier28_4pc&active_enemies>1&talent.inferno.enabled
actions.havoc+=/shadowburn
actions.havoc+=/incinerate,if=cast_time<havoc_remains