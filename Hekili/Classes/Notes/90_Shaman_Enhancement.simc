## Enhancement Shaman
## August 23, 2022

actions.precombat=windfury_weapon,if=buff.windfury_weapon.remains<300
actions.precombat+=/flametongue_weapon,if=buff.flametongue_weapon.remains<300
actions.precombat+=/bottled_flayedwing_toxin
actions.precombat+=/lightning_shield,if=buff.lightning_shield.remains<300
actions.precombat+=/stormkeeper,if=talent.stormkeeper.enabled
actions.precombat+=/windfury_totem,if=!runeforge.doom_winds.equipped&buff.windfury_totem.down
actions.precombat+=/fleshcraft,if=soulbind.pustule_eruption|soulbind.volatile_solvent
actions.precombat+=/variable,name=trinket1_is_weird,value=trinket.1.is.the_first_sigil|trinket.1.is.scars_of_fraternal_strife|trinket.1.is.cache_of_acquired_treasures
actions.precombat+=/variable,name=trinket2_is_weird,value=trinket.2.is.the_first_sigil|trinket.2.is.scars_of_fraternal_strife|trinket.2.is.cache_of_acquired_treasures

## Executed every time the actor is available.
actions=bloodlust
# In-combat potion is before combat ends.
actions+=/potion,if=(talent.ascendance.enabled&raid_event.adds.in>=90&cooldown.ascendance.remains<10)|(talent.hot_hand.enabled&buff.molten_weapon.up)|buff.icy_edge.up|(talent.stormflurry.enabled&buff.crackling_surge.up)|debuff.earthen_spike.up|active_enemies>1|boss&fight_remains<30
# Interrupt
actions+=/wind_shear
actions+=/use_item,name=the_first_sigil,if=(talent.ascendance.enabled&raid_event.adds.in>=90&cooldown.ascendance.remains<10)|(talent.hot_hand.enabled&buff.molten_weapon.up)|buff.icy_edge.up|(talent.stormflurry.enabled&buff.crackling_surge.up)|debuff.earthen_spike.up|active_enemies>1|fight_remains<30
actions+=/use_item,name=cache_of_acquired_treasures,if=buff.acquired_sword.up|fight_remains<25
actions+=/use_item,name=scars_of_fraternal_strife,if=!buff.scars_of_fraternal_strife_4.up|fight_remains<31|raid_event.adds.in<16|active_enemies>1
actions+=/trinket1,if=!variable.trinket1_is_weird
actions+=/trinket2,if=!variable.trinket2_is_weird
actions+=/use_items
actions+=/blood_fury,if=!talent.ascendance.enabled|buff.ascendance.up|cooldown.ascendance.remains>50
actions+=/berserking,if=!talent.ascendance.enabled|buff.ascendance.up
actions+=/fireblood,if=!talent.ascendance.enabled|buff.ascendance.up|cooldown.ascendance.remains>50
actions+=/ancestral_call,if=!talent.ascendance.enabled|buff.ascendance.up|cooldown.ascendance.remains>50
actions+=/bag_of_tricks,if=!talent.ascendance.enabled|!buff.ascendance.up
actions+=/feral_spirit
actions+=/fae_transfusion,if=(talent.ascendance.enabled|runeforge.doom_winds.equipped)&(soulbind.grove_invigoration|soulbind.field_of_blossoms|active_enemies=1)
actions+=/ascendance,if=raid_event.adds.in>=90|active_enemies>1
actions+=/windfury_totem,line_cd=15,if=runeforge.doom_winds.equipped&buff.doom_winds_debuff.down&(raid_event.adds.in>=60|active_enemies>1)|buff.windfury_totem.down&(!runeforge.doom_winds.equipped|buff.doom_winds_debuff.up)
# If only one enemy, priority follows the 'single' action list.
actions+=/call_action_list,name=single,strict=1,if=active_enemies=1
# On multiple enemies, the priority follows the 'aoe' action list.
actions+=/call_action_list,name=aoe,strict=1,if=active_enemies>1
actions+=/counterstrike_totem
actions+=/skyfury_totem
actions+=/primal_strike

## Multi target action priority list
actions.aoe=chain_harvest,if=buff.maelstrom_weapon.stack>=5
actions.aoe+=/crash_lightning,if=runeforge.doom_winds.equipped&buff.doom_winds.up
actions.aoe+=/sundering,if=runeforge.doom_winds.equipped&buff.doom_winds.up
actions.aoe+=/healing_stream_totem,if=runeforge.raging_vesper_vortex.equipped&talent.earth_shield.enabled&vesper_totem_heal_charges>0
actions.aoe+=/earth_shield,if=runeforge.raging_vesper_vortex.equipped&talent.earth_shield.enabled&vesper_totem_heal_charges>0
actions.aoe+=/fire_nova,if=active_dot.flame_shock>=6|(active_dot.flame_shock>=4&active_dot.flame_shock>=cycle_enemies)
actions.aoe+=/primordial_wave,cycle_targets=1,if=!buff.primordial_wave.up
actions.aoe+=/windstrike,if=runeforge.deeply_rooted_elements.equipped&buff.crash_lightning.up
actions.aoe+=/stormstrike,if=runeforge.deeply_rooted_elements.equipped&buff.crash_lightning.up
actions.aoe+=/lava_lash,cycle_targets=1,if=dot.flame_shock.ticking&(active_dot.flame_shock<cycle_enemies&active_dot.flame_shock<6)
actions.aoe+=/flame_shock,if=!ticking
actions.aoe+=/flame_shock,cycle_targets=1,if=!talent.hailstorm.enabled&active_dot.flame_shock<cycle_enemies&active_dot.flame_shock<6
actions.aoe+=/lightning_bolt,if=(active_dot.flame_shock>=active_enemies|active_dot.flame_shock>=4)&buff.primordial_wave.up&buff.maelstrom_weapon.stack>=5&(!buff.splintered_elements.up|fight_remains<=12|raid_event.adds.remains<=gcd)
actions.aoe+=/frost_shock,if=buff.hailstorm.up
actions.aoe+=/fae_transfusion,if=soulbind.grove_invigoration|soulbind.field_of_blossoms|runeforge.seeds_of_rampant_growth.equipped
actions.aoe+=/crash_lightning,if=buff.crash_lightning.down&buff.primordial_wave.up&buff.maelstrom_weapon.stack<5
actions.aoe+=/sundering,if=buff.primordial_wave.up&buff.maelstrom_weapon.stack<5
actions.aoe+=/stormstrike,if=buff.primordial_wave.up&buff.maelstrom_weapon.stack<5
actions.aoe+=/sundering
actions.aoe+=/fire_nova,if=active_dot.flame_shock>=4
actions.aoe+=/crash_lightning,if=talent.crashing_storm.enabled|buff.crash_lightning.down
actions.aoe+=/lava_lash,cycle_targets=1,if=talent.lashing_flames.enabled
actions.aoe+=/fire_nova,if=active_dot.flame_shock>=3
actions.aoe+=/vesper_totem
actions.aoe+=/chain_lightning,if=buff.stormkeeper.up
actions.aoe+=/lava_lash,if=buff.crash_lightning.up
actions.aoe+=/elemental_blast,if=buff.maelstrom_weapon.stack>=5
actions.aoe+=/stormkeeper,if=buff.maelstrom_weapon.stack>=5
actions.aoe+=/chain_lightning,if=buff.maelstrom_weapon.stack=10
actions.aoe+=/stormstrike,if=buff.crash_lightning.up
actions.aoe+=/fire_nova,if=active_dot.flame_shock>=2
actions.aoe+=/crash_lightning
actions.aoe+=/windstrike
actions.aoe+=/stormstrike
actions.aoe+=/fleshcraft,interrupt=1,if=soulbind.volatile_solvent
actions.aoe+=/flame_shock,cycle_targets=1,if=refreshable
actions.aoe+=/fae_transfusion
actions.aoe+=/frost_shock
actions.aoe+=/chain_lightning,if=buff.maelstrom_weapon.stack>=5
actions.aoe+=/earthen_spike
actions.aoe+=/earth_elemental
actions.aoe+=/windfury_totem,if=buff.windfury_totem.remains<30

## Single target action priority list
actions.single=windstrike
actions.single+=/lava_lash,if=buff.hot_hand.up|(runeforge.primal_lava_actuators.equipped&buff.primal_lava_actuators.stack>6)
actions.single+=/windfury_totem,if=!buff.windfury_totem.up
actions.single+=/stormstrike,if=runeforge.doom_winds.equipped&buff.doom_winds.up
actions.single+=/crash_lightning,if=runeforge.doom_winds.equipped&buff.doom_winds.up
actions.single+=/ice_strike,if=runeforge.doom_winds.equipped&buff.doom_winds.up
actions.single+=/sundering,if=runeforge.doom_winds.equipped&buff.doom_winds.up
actions.single+=/primordial_wave,if=buff.primordial_wave.down&(raid_event.adds.in>42|raid_event.adds.in<6)
actions.single+=/flame_shock,if=!ticking
actions.single+=/lightning_bolt,if=buff.maelstrom_weapon.stack>=5&buff.primordial_wave.up&raid_event.adds.in>buff.primordial_wave.remains&(!buff.splintered_elements.up|fight_remains<=12)
actions.single+=/vesper_totem,if=raid_event.adds.in>40
actions.single+=/frost_shock,if=buff.hailstorm.up
actions.single+=/earthen_spike
actions.single+=/lava_lash,if=dot.flame_shock.refreshable
actions.single+=/fae_transfusion,if=!runeforge.seeds_of_rampant_growth.equipped|cooldown.feral_spirit.remains>30
actions.single+=/stormstrike,if=talent.stormflurry.enabled&buff.stormbringer.up
actions.single+=/chain_lightning,if=buff.stormkeeper.up
actions.single+=/elemental_blast,if=buff.maelstrom_weapon.stack>=5
actions.single+=/healing_stream_totem,if=runeforge.raging_vesper_vortex.equipped&talent.earth_shield.enabled&(vesper_totem_heal_charges>1|(vesper_totem_heal_charges>0&raid_event.adds.in>(buff.vesper_totem.remains-3)))
actions.single+=/earth_shield,if=runeforge.raging_vesper_vortex.equipped&talent.earth_shield.enabled&(vesper_totem_heal_charges>1|(vesper_totem_heal_charges>0&raid_event.adds.in>(buff.vesper_totem.remains-3)))
actions.single+=/chain_harvest,if=buff.maelstrom_weapon.stack>=5&raid_event.adds.in>=90
actions.single+=/lightning_bolt,if=buff.maelstrom_weapon.stack=10&buff.primordial_wave.down
actions.single+=/stormstrike
actions.single+=/stormkeeper,if=buff.maelstrom_weapon.stack>=5
actions.single+=/fleshcraft,interrupt=1,if=soulbind.volatile_solvent
actions.single+=/windfury_totem,if=buff.windfury_totem.remains<10
actions.single+=/lava_lash
actions.single+=/lightning_bolt,if=buff.maelstrom_weapon.stack>=5&buff.primordial_wave.down
actions.single+=/sundering,if=raid_event.adds.in>=40
actions.single+=/frost_shock
actions.single+=/crash_lightning
actions.single+=/ice_strike
actions.single+=/fire_nova,if=active_dot.flame_shock
actions.single+=/fleshcraft,if=soulbind.pustule_eruption
actions.single+=/earth_elemental
actions.single+=/flame_shock,if=settings.filler_shock
actions.single+=/windfury_totem,if=buff.windfury_totem.remains<30